buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = "https://maven.explodingcreeper.me" }
        maven { url = "https://maven.parchmentmc.org" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'org.spongepowered:mixingradle:0.7.+'
        //classpath 'me.hypherionmc:CurseGradle:2.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: "org.parchmentmc.librarian.forgegradle"
apply plugin: "maven-publish"
apply plugin: 'org.spongepowered.mixin'
//apply plugin: 'me.hypherionmc.cursegradle'

group = 'me.hypherionmc'
version = mod_version
archivesBaseName = 'HyperLighting-1.18.1'

// Jenkins
if (System.getenv('BUILD_NUMBER') != null) {
    version += "." + System.getenv('BUILD_NUMBER') + "d"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    //mappings channel: 'official', version: '1.18.1'
    mappings channel: 'parchment', version: '2021.12.19-1.18.1'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=hyperlighting.mixins.json"

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                hyperlighting {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                hyperlighting {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'hyperlighting', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                hyperlighting {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven { url = "https://maven.hypherionmc.me" }
    maven { url = "https://dvs1.progwml6.com/files/maven/" }
    maven { url = "https://maven.tterrag.com/" }
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.k-4u.nl" }
}

dependencies {

    minecraft 'net.minecraftforge:forge:1.18.1-39.0.9'

    // The new Hyper Lighting Core - Now known as RGBLib
    /*implementation "me.hypherionmc:RGBLib:${rgblib_version}:api"
    runtimeOnly "me.hypherionmc:RGBLib:${rgblib_version}"*/

    //implementation "mezz.jei:jei-1.18.1:${jei_version}"
    implementation "mcjty.theoneprobe:theoneprobe:${top_version}:api"
    implementation "me.shedaniel.cloth:cloth-config-forge:${cconfig_version}"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "hyperlighting",
                "Specification-Vendor"    : "HypherionSA",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "HypherionSA",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "hyperlighting.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

task apiJar(type: Jar) {
    classifier = "api"
    from(sourceSets.main.output) {
        include "me/hypherionmc/hyperlighting/api/**"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(apiJar) {
                builtBy apiJar
            }
            artifact(jar) {
                builtBy jar
            }
        }
    }

    repositories {
        maven {
            url project.mavenUrl
            credentials {
                username System.getenv('MAVEN_USER')
                password System.getenv('MAVEN_PASS')
            }
        }
    }
}

/*curseforge {
    if (System.getenv('CURSE_KEY') != null) {
        apiKey = System.getenv('CURSE_KEY')
    } else {
        apiKey = '123'
    }
    options {
        forgeGradleIntegration = true
    }
    project {
        id = '375922'
        changelog = file('changelog.md')
        changelogType = 'markdown'
        releaseType = 'release'
        addGameVersion("Java 17")
        addGameVersion("1.18")
        addGameVersion("Forge")
        mainArtifact(jar) {
            displayName = "Hyper Lighting Forge 1.18 $project.version"
        }
    }
}*/

mixin {
    add sourceSets.main, "hyperlighting.refmap.json"
    config 'hyperlighting.mixins.json'
}
